\begin{figure}
\begin{verbatim}
labels = <many { @length uint8 | 1..255
                 label n_of @length uint8 }
          uint8 = 0>
compressed ={
  $decompressed transform dnscompress($current);
  labels apply $decompressed labels
}
question = {
  labels compressed_lbls
  qtype uint16 | 1..16
  qclass uint16 | [1,255]
}
answer = {
  labels labels
  rtype uint16 | 1..16
  class uint16 | [1]
  ttl uint32
  @rlength uint16
  rdata n_of @rlength uint8
}
dnspacket = {
  id uint16
  qr uint1
  opcode uint4
  aa uint1
  tc uint1
  rd uint1
  ra uint1
  uint3 = 0
  rcode uint4
  @qc uint16
  @ac uint16
  uint16 = 0    // authority
  uint16 = 0    // additional
  // We don't support authority or
  // additional sections in the prototype
  questions n_of @qc question
  responses n_of @ac answer
}
\end{verbatim}
\caption{Nail grammar for DNS packets, used by our prototype DNS server.}
\label{fig:dns-full}
\end{figure}
