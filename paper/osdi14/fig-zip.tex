\begin{figure}
\begin{verbatim}
!LITTLE-ENDIAN
fileentry(@crc32 uint32,
  @compression_method uint16,
  @compressed_size uint32, 
  @uncompressed_size uint32) = { 
     uint32 = 0x04034b50
     version uint16
     flags file_flags
     @compression_method_local uint16
//cut other redundant information 
     $compressed transform size_u32 ($current @compressed_size)
     $uncompressed transform zip_compression ($compressed @compression_method )
     transform crc_32 ($uncompressed @crc32)
     contents apply $uncompressed many uint8
     transform uint16_depend (@compression_method_local @compression_method)
//cut
}
dir_fileheader($file) = {
  uint32 = 0x02014b50
  os uint16
  pkzip_version uint16
  flags file_flags
  @compression_method uint16      
  mtime uint16
  mdate uint16
  @crc32 uint32
  @compressed_size uint32
  @uncompressed_size uint32
  @file_name_len uint16
  @extra_len uint16
  @comment_len uint16
  disk uint16 | [0] // No multi-disk support
  internal internal_attr
  external_attr uint32
  @off uint32
  filename n_of @file_name_len uint8
  extra_field n_of @extra_len uint8
  comment n_of @comment_len uint8
  $content transform offset_u32 ($file @off)
  contents apply $content fileentry(@crc32,@compression_method,
                                    @compressed_size, @uncompressed_size)
}

end_of_directory($file) = { 
                 uint32 = 0x06054b50
                 disks uint16 | [0]
                 directory_disk uint16 | [0]
                 @this_disk_directory_records uint16
                 @total_directory_records uint16
                 @directory_size uint32 
                 @directory_start uint32
                 $dirstr1 transform offset_u32 ($filestream @directory_start) //TODO: how can we get the offset properly
                 $directory_stream transform size_u32 ($dirstr1 @directory_size)
                 @comment_length uint16
                 comment n_of @comment_length uint8
                 files apply $directory_stream n_of @total_directory_records dir_fileheader($file)
                 transform uint16_depend (@this_disk_directory_records @total_directory_records)
}
zip_file = { 
         $file, $end_directory transform zip_end_of_directory ($current)
         contents apply $end_directory end_of_directory($file)
}
\end{verbatim}
\caption{Extracted Nail grammar for ZIP files.}
\label{fig:zip-extract}
\end{figure}
